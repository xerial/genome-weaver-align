@REM ----------------------------------------------------------------------------
@REM Licensed to the Apache Software Foundation (ASF) under one
@REM or more contributor license agreements.  See the NOTICE file
@REM distributed with this work for additional information
@REM regarding copyright ownership.  The ASF licenses this file
@REM to you under the Apache License, Version 2.0 (the
@REM "License"); you may not use this file except in compliance
@REM with the License.  You may obtain a copy of the License at
@REM
@REM    http://www.apache.org/licenses/LICENSE-2.0
@REM
@REM Unless required by applicable law or agreed to in writing,
@REM software distributed under the License is distributed on an
@REM "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
@REM KIND, either express or implied.  See the License for the
@REM specific language governing permissions and limitations
@REM under the License.
@REM ----------------------------------------------------------------------------

@REM ----------------------------------------------------------------------------
@REM Genome Weaver Start Up Batch script
@REM This script was generated by modifying the Maven's start up script
@REM
@REM Required ENV vars:
@REM JAVA_HOME - location of a JDK home dir
@REM
@REM ----------------------------------------------------------------------------

@REM Begin all REM lines with '@' in case MAVEN_BATCH_ECHO is 'on'
@echo off
@REM enable echoing my setting MAVEN_BATCH_ECHO to 'on'
@if "%MAVEN_BATCH_ECHO%" == "on"  echo %MAVEN_BATCH_ECHO%

@REM set %HOME% to equivalent of $HOME
if "%HOME%" == "" (set HOME=%HOMEDRIVE%%HOMEPATH%)

set ERROR_CODE=0

@REM set local scope for the variables with windows NT shell
if "%OS%"=="Windows_NT" @setlocal

@REM ==== START VALIDATION ====
:chkMHome

if "%OS%"=="Windows_NT" SET GW_HOME=%~dp0\..
if not "%GW_HOME%"=="" goto valMHome

echo ERROR: GW_HOME not found in your environment.
echo Please set the GW_HOME variable in your environment to match the
echo location of the GW installation
echo.
goto error

:valMHome
if exist "%GW_HOME%\bin\gw.bat" goto init

echo.
echo ERROR: GW_HOME is set to an invalid directory.
echo GW_HOME = %GW_HOME%
echo Please set the GW_HOME variable in your environment to match the
echo location of the GW installation
echo.
goto error
@REM ==== END VALIDATION ====

:init
@REM Decide how to startup depending on the version of windows

@REM -- Win98ME
if NOT "%OS%"=="Windows_NT" goto Win9xArg

@REM -- 4NT shell
if "%@eval[2+2]" == "4" goto 4NTArgs

@REM -- Regular WinNT shell
set GW_CMD_LINE_ARGS=%*
goto endInit

@REM The 4NT Shell from jp software
:4NTArgs
set GW_CMD_LINE_ARGS=%$
goto endInit

:Win9xArg
@REM Slurp the command line arguments.  This loop allows for an unlimited number
@REM of agruments (up to the command line limit, anyway).
set GW_CMD_LINE_ARGS=
:Win9xApp
if %1a==a goto endInit
set GW_CMD_LINE_ARGS=%GW_CMD_LINE_ARGS% %1
shift
goto Win9xApp

@REM Reaching here means variables are defined and arguments have been captured
:endInit
SET GW_JAVA_EXE="java.exe"

@REM -- 4NT shell
if "%@eval[2+2]" == "4" goto 4NTCWJars


@REM Start GW
:runm2
%GW_JAVA_EXE% -classpath %GW_HOME%\boot\plexus-classworlds-*.jar -Dclassworlds.conf="%GW_HOME%\bin\gw.conf" -Dgw.home="%GW_HOME%" org.codehaus.plexus.classworlds.launcher.Launcher %GW_CMD_LINE_ARGS%
if ERRORLEVEL 1 goto error
goto end

:error
if "%OS%"=="Windows_NT" @endlocal
set ERROR_CODE=1

:end
@REM set local scope for the variables with windows NT shell
if "%OS%"=="Windows_NT" goto endNT

@REM For old DOS remove the set variables from ENV - we assume they were not set
@REM before we started - at least we don't leave any baggage around
set GW_JAVA_EXE=
set GW_CMD_LINE_ARGS=
goto postExec

:endNT
@endlocal

:postExec
exit /B %ERROR_CODE%

